buildscript {
    ext {
        lintVersion = '26.1.4'
    }

    repositories {
        google()
        jcenter()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.0'
    }
}

    allprojects {
    repositories {
        google()
        jcenter()
    }
}

apply plugin: "com.android.application"

ext {
    compileSdkVersion = 28
    minSdkVersion = 14
    buildToolsVersion = "28.0.3"
    supportLibVersion = "28.0.0"

    appVersion = 20
}

dependencies {
    implementation "com.android.support:support-compat:${rootProject.ext.supportLibVersion}"
    implementation "com.android.support:appcompat-v7:${rootProject.ext.supportLibVersion}"
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    androidTestUtil 'com.android.support.test:orchestrator:1.0.2'
    lintChecks project(':idCheck')

}


android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.compileSdkVersion

        applicationId 'me.tsukanov.counter'
        versionCode rootProject.ext.appVersion
        versionName "${rootProject.ext.appVersion}"
        testInstrumentationRunner = 'android.support.test.runner.AndroidJUnitRunner'

        // The following argument makes the Android Test Orchestrator run its
        // "pm clear" command after each test invocation. This command ensures
        // that the app's state is completely cleared between tests.
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }

    testOptions {
        execution 'ANDROID_TEST_ORCHESTRATOR'
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt')

            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
            pseudoLocalesEnabled false
            zipAlignEnabled true
            shrinkResources true
        }
    }
}
task clearData(type: Exec) {
    def clearDataCommand = ['adb', 'shell', 'pm', 'clear', 'me.tsukanov.counter']
    commandLine clearDataCommand
}